plugins {
    id 'base'
}

group 'de.dfki.mary'
version '0.1.0-SNAPSHOT'
description 'Upper Sorbian voice data for MaryTTS'

repositories {
    mavenLocal()

    exclusiveContent {
        forRepository {
            ivy {
                url 'https://digital.serbski-institut.de/marytts'
                patternLayout {
                    artifact '[module].[ext]'
                }
                metadataSources {
                    artifact()
                }
            }
        }
        filter {
            includeGroup 'de.serbski-institut'
        }
    }
}

configurations {
    data
}

dependencies {
    data group: 'de.serbski-institut', name: 'hsb_audio_data', version: '2022.10.10', ext: 'zip'
}

def unpackDataTask = tasks.register('unpackData') {
    ext.textDir = objects.directoryProperty()
            .convention(layout.buildDirectory.dir('text'))
    ext.wavDir = objects.directoryProperty()
            .convention(layout.buildDirectory.dir('wav'))
    outputs.dir textDir
    outputs.dir wavDir

    doLast {
        copy {
            into temporaryDir
            from configurations.data
            filesMatching '*.zip', { zipDetails ->
                copy {
                    into textDir
                    from zipTree(zipDetails.file)
                    include 'text/*.txt'
                    eachFile { it.path = it.name }
                    includeEmptyDirs = false
                }
                copy {
                    into wavDir
                    from zipTree(zipDetails.file)
                    include 'wav/*.wav'
                    eachFile { it.path = it.name }
                    includeEmptyDirs = false
                }
            }
        }
    }
}
